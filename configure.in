#############################################################################
##                                                                         ##
##                              configure.in                               ##
##                                                                         ##
##                        Matching Pursuit Library                         ##
##                                                                         ##
## Rémi Gribonval                                                          ##
## Sacha Krstulovic                                        Mon Feb 21 2005 ##
## ----------------------------------------------------------------------- ##
##                                                                         ##
##  Copyright (C) 2005 IRISA                                               ##
##                                                                         ##
##  This program is free software; you can redistribute it and/or modify   ##
##  it under the terms of the GNU General Public License as published by   ##
##  the Free Software Foundation; either version 2, or (at your option)    ##
##  any later version.                                                     ##
##                                                                         ##
##  This program is distributed in the hope that it will be useful,        ##
##  but WITHOUT ANY WARRANTY; without even the implied warranty of         ##
##  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the          ##
##  GNU General Public License for more details.                           ##
##                                                                         ##
##  You should have received a copy of the GNU General Public License      ##
##  along with this program; if not, write to the Free Software            ##
##  Foundation, Inc., 59 Temple Place - Suite 330,                         ##
##  Boston, MA 02111-1307, USA.                                            ##
##                                                                         ##
#############################################################################
##
## SVN log:
##
##   $Author$
##   $Date$
##   $Revision$
##

VERSION="0.5beta"

dnl Process this file with autoconf to produce a configure script.
AC_INIT(mptk,$VERSION)

dnl Check for architecture
AC_CANONICAL_TARGET

dnl Automake stuff
AM_INIT_AUTOMAKE(mptk,$VERSION)
AM_CONFIG_HEADER(config.h)
AC_REVISION($Revision$)

dnl Checks for usefuls programs (like a compiler, a precompiler, a linker...)
AC_PROG_INSTALL
AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_CXX
AM_PROG_LEX
#AM_PROG_LIBTOOL
AC_PROG_RANLIB

dnl Check for sizeof and byte-order of the standard types
AC_CHECK_SIZEOF(unsigned int)
AC_CHECK_SIZEOF(unsigned short int)
AC_CHECK_SIZEOF(unsigned long int)
AC_CHECK_SIZEOF(float)
AC_CHECK_SIZEOF(double)
AC_CHECK_SIZEOF(void*)
AC_C_BIGENDIAN

dnl Checks for header files
AC_HEADER_STDC
AC_CHECK_HEADERS(stdarg.h stdlib.h math.h limits.h assert.h)


dnl ------------------------------------------------------------
dnl -- Compilation options
dnl ------------------------------------------------------------

dnl -----------------
dnl -- Default values
dnl -----------------
CC="gcc"
CFLAGS="-O3 -Wall -W"

CXX="g++"
CXXFLAGS="-O3 -Wall -Wno-deprecated -W"

dnl ---------------------------------------------------
dnl -- Add debugging options to the compilation process
dnl ---------------------------------------------------
AC_ARG_ENABLE(debug,
[  --enable-debug          Switches on -g to allow debugging],
if test $enableval = "yes" ; then
  CXXFLAGS="$CXXFLAGS -g"
  CFLAGS="$CFLAGS -g"
fi)

dnl ---------------------------------------------------
dnl -- Add profiling options to the compilation process
dnl ---------------------------------------------------
AC_ARG_ENABLE(profile,
[  --enable-profile        Switches on -g -pg to allow profiling],
if test $enableval = "yes" ; then
   CXXFLAGS="$CXXFLAGS -g -pg";
   CFLAGS="$CFLAGS -g -pg"
fi)

dnl ---------------------------------------------------
dnl -- Force the removal of debugging output and assertions
dnl ---------------------------------------------------
AC_ARG_ENABLE(ndebug,
[  --disable-ndebug        Removes the default -DNDEBUG switch (to get the assertions and some debugging output)],
if test $enableval = "yes" ; then
   CXXFLAGS="$CXXFLAGS -DNDEBUG";
   CFLAGS="$CFLAGS -DNDEBUG"
fi,
CXXFLAGS="$CXXFLAGS -DNDEBUG";
CFLAGS="$CFLAGS -DNDEBUG"
)

dnl ---------------------------------------------------
dnl -- Force the static linking of external libraries
dnl ---------------------------------------------------
AC_ARG_ENABLE(static,
[  --enable-static         If enabled, forces the static linking of external libraries],
if test $enableval = "yes" ; then
  LDFLAGS="$LDFLAGS -static"
fi)


dnl ------------------------------------------------------------
dnl -- Export some locations for created includes and libs
dnl ------------------------------------------------------------

MP_SYSTEM_H_DIR='$(top_srcdir)'
DSP_WIN_H_DIR='$(top_srcdir)/src/libdsp_windows'
MPTK_H_DIR='$(top_builddir)/src/libmptk'

INCLUDES="$INCLUDES -I$MP_SYSTEM_H_DIR -I$DSP_WIN_H_DIR -I$MPTK_H_DIR"

LOCAL_BUILD_LIBS='$(top_builddir)/src/libmptk/libmptk.a $(top_builddir)/src/libdsp_windows/libdsp_windows.a'
AC_SUBST(LOCAL_BUILD_LIBS)


dnl ------------------------------------------------------------
dnl check for external libraries (FFTW and libsndfile) and
dnl add the corresponding options to LIBS and CXXFLAGS
dnl ------------------------------------------------------------

AC_ARG_WITH(libs,
	[  --with-libs            tell where extra libraries (such as fftw3 and libsndfile) are installed on your system],
	[ LIBS="$LIBS -L$with_libs/lib"
	  INCLUDES="$INCLUDES -I$with_libs/include" ]
)

AC_ARG_WITH(fftw3,
	[  --with-fftw3            tell specifically where fftw3 was installed (overrides --with-libs)],
	[ LIBS="$LIBS -L$with_fftw3/lib"
	  INCLUDES="$INCLUDES -I$with_fftw3/include" ]
)

AC_ARG_WITH(libsndfile,
	[  --with-libsndfile       tell specifically where libsndfile was installed (overrides --with-libs)],
	[ LIBS="$LIBS -L$with_libsndfile/lib"
	  INCLUDES="$INCLUDES -I$with_libsndfile/include" ]
)

AC_CHECK_LIB(fftw3,main,[AC_DEFINE(HAVE_FFTW3,1,"Turn on the FFTW3 support.")],
	[AC_MSG_ERROR([FFTW3 library not found -- Please make sure that FFTW3 is installed on your system and/or indicate its location with the switch: --with-fftw3=PATH-TO-FFTW3. (See configure --help.)])]
)

AC_CHECK_LIB(sndfile,main,[AC_DEFINE(HAVE_SNDFILE,1,"Turn on the LIBSNDFILE support.")],
	[AC_MSG_ERROR(LIBSNDFILE library not found --  Please make sure that LIBSNDFILE is installed on your system and/or indicate its location with the switch: --with-libsndfile=PATH-TO-LIBSNDFILE. (See configure --help.) )],
)

LIBS="$LIBS -lm -lfftw3 -lsndfile"


dnl ------------------------------------------------------------
dnl PROGRAM CHECKS
dnl ------------------------------------------------------------


dnl ------------------------------------------------------------
dnl check for PERL
dnl ------------------------------------------------------------
AC_PATH_PROG(PERLPATH,perl,/usr/bin/perl,no)
if test "x$PERLPATH" = "xno"; then
   AC_MSG_WARN([perl was not found on your system.])
fi

dnl ------------------------------------------------------------
dnl -- Enable/disable documentation generation
dnl ------------------------------------------------------------
DOC_DIR=""

AC_ARG_ENABLE(doc,
[  --enable-doc            Enables the re-generation of the documentation],
        DOCUMENTATION_MODE=$enableval, DOCUMENTATION_MODE=no)

if test "x$DOCUMENTATION_MODE" = "xyes" ; then
   dnl -- Use the doc directory
   DOC_DIR="doc"
   dnl -- check for DOXYGEN
   AC_CHECK_PROG(DOXYGEN,doxygen,doxygen,no)
   if test "x$DOXYGEN" = "xno"; then
      AC_MSG_WARN([doxygen was not found on your system. Can't rebuild the reference manual.])
   fi
   dnl -- check for LATEX
   AC_CHECK_PROG(LATEX,latex,latex,no)
   if test "x$LATEX" = "xno"; then
      AC_MSG_WARN([latex was not found on your system. Can't compile any of the docs.])
   fi
   dnl -- check for DVIPS
   AC_CHECK_PROG(DVIPS,dvips,dvips,no)
   if test "x$DVIPS" = "xno"; then
      AC_MSG_WARN([dvips was not found on your system. Can't generate postscript manuals from their dvi version.])
   fi
   dnl -- check for PS2PDF
   AC_CHECK_PROG(PS2PDF,ps2pdf,ps2pdf,no)
   if test "x$PS2PDF" = "xno"; then
      AC_MSG_WARN([ps2pdf was not found on your system. Can't generate pdf manuals from their postscript version.])
   fi
   dnl -- Prepare for the output of relevant Makefiles
   AC_CONFIG_FILES( doc/Makefile                    \
		    doc/refman/doxygen_config       \
		    doc/refman/Makefile             \
		    doc/userman/Makefile            \
		    doc/short_description/Makefile  )
else
  AC_MSG_NOTICE([The re-generation of the doc is disabled by default.
  If pre-compiled docs (in pdf and/or postscript) are not included in this package,
  you can download them from:
                 http://mptk.gforge.inria.fr
  If you need to re-generate the docs yourself, use --enable-doc (see configure --help).])
fi

AC_SUBST(DOC_DIR)

dnl ------------------
dnl -- Clean the house
dnl ------------------

dnl -- Export relevant info
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(CXX)
AC_SUBST(CXXFLAGS)
AC_SUBST(INCLUDES)
AC_SUBST(LDFLAGS)

dnl -- Export the relevant Makefiles
AC_CONFIG_FILES( Makefile                        \
		 src/Makefile                    \
		 src/libdsp_windows/Makefile     \
		 src/libmptk/Makefile            \
		 src/utils/Makefile              \
		 src/tests/Makefile              \
		 src/tests/test_mpcycle.sh       \
		 src/tests/signals/Makefile      \
		 src/tests/ref/Makefile          \
		 extras/Makefile                 \
		 extras/matlab/Makefile          )

dnl -- Perform the final AC_OUTPUT
AC_OUTPUT

dnl ------------------------------------------------------------
